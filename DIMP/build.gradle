plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'signing'
    id "com.vanniktech.maven.publish" version "0.34.0"
}

group = 'chat.dim'
version = '2.3.0'
//version '1.0-SNAPSHOT'

def mod = 'DIMP'
def desc = 'New Protocol designed for decentralized instant messaging.'

def siteUrl = 'https://github.com/dimchat/core-java'
def gitUrl = 'https://github.com/dimchat/core-java.git'

def connUrl = 'scm:git:git://github.com/dimchat/core-java.git'
def devUrl = 'scm:git:ssh://github.com/dimchat/core-java.git'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
}

dependencies {
    api group: 'chat.dim', name: 'DaoKeDao', version: '2.2.0'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        links = ['https://docs.oracle.com/javase/7/docs/api']
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

mavenPublishing {
    coordinates(group, mod, version)

    pom {
        packaging = 'jar'
        name = mod
        description = desc
        inceptionYear = "2018"
        url = siteUrl

        licenses {
            license {
                name = 'MIT'
                url = 'https://mit-license.org'
                distribution = 'https://mit-license.org'
            }
        }

        developers {
            developer {
                id = 'moky'
                name = 'Albert Moky'
                email = 'albert.moky@gmail.com'
                roles = ['architect']
                organization = 'DIM Group'
            }
        }

        scm {
            url = siteUrl
            connection = connUrl
            developerConnection = devUrl
        }
    }
}
